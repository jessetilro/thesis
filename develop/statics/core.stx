module statics/core

imports signatures/develop-sig
imports statics/_constructors

signature

  relations
    entity : ID -> TYPE
    entity_extension : ID * scope
    prop_extension : scope * scope
    member : ID -> (RICHTYPE * PROPS)
    function : ID -> (RICHTYPE * PROPS)
    implicit : string -> (RICHTYPE * PROPS)
    prop : string -> PROPVAL
    
  name-resolution
    labels 
      // D // definition
      I // import
      J // lower priority import
      P // parent
      S // requires a J to be wellformed
      // N // no edge at all (used for inverse querying)
      // new experimental labels, inspired by webdsl
      PARENT
      INHERIT
      EXTEND
//      DECLARE
  
rules // syntax

  nameOfSpaceArrowIDList : list(SpaceArrowID) -> ID
  nameOfSpaceArrowIDList([x]) = nameOfSpaceArrowID(x).
  nameOfSpaceArrowIDList([]) = "".
  nameOfSpaceArrowID : SpaceArrowID -> ID
  nameOfSpaceArrowID(ID2SpaceArrowID(x)) = x.
  
  nameOfDotIDList : list(DotID) -> ID
  nameOfDotIDList([x]) = nameOfDotID(x).
  nameOfDotIDList([]) = "".
  nameOfDotID : DotID -> ID
  nameOfDotID(ID2DotID(x)) = x.
  
  nameOfIDSpaceList : list(IDSpace) -> ID
  nameOfIDSpaceList([x]) = ameOfIDSpace(x).
  nameOfIDSpaceList([]) = "".
  ameOfIDSpace : IDSpace -> ID
  ameOfIDSpace(ID2IDSpace(x)) = x.
  
rules // multiplicity/ordering lattice

  //                 TZeroOrMore()
  //                  /    \
  // ZeroOrMoreOrdered()  TOneOrMore()
  //             /    \    /
  //  TZeroOrOne()  TOneOrMoreOrdered()
  //             \    /
  //             TOne()
  
  multOfMultList : list(Multiplicity) -> MULT
  
  multOfMultList([x]) = multOfMult(x).
  multOfMultList([]) = ONE(). // todo check default multiplicity
  
  multOfMult : Multiplicity -> MULT
  
  multOfMult(One()) = ONE().
  multOfMult(ZeroOrOne()) = ZERO_ONE().
  multOfMult(OneOrMore()) = ONE_MORE().
  multOfMult(ZeroOrMore()) = ZERO_MORE().
  
  multOfSpaceMultList : list(SpaceMultiplicity) -> MULT
  
  multOfSpaceMultList([x]) = multOfSpaceMultiplicity(x).
  multOfSpaceMultList([]) = ONE().
  
  multOfSpaceMultiplicity : SpaceMultiplicity -> MULT
  multOfSpaceMultiplicity(MultiplicityOrdering2SpaceMultiplicity(x)) = multOfMultOrd(x).
  
  multOfMultOrdList : list(MultiplicityOrdering) -> MULT
  
  multOfMultOrdList([x]) = multOfMultOrd(x).
  multOfMultOrdList([]) = ONE(). // todo check default multiplicity ordering
  
  multOfMultOrd : MultiplicityOrdering -> MULT
  
  multOfMultOrd(Multiplicity2MultiplicityOrdering(x)) = multOfMult(x).
  multOfMultOrd(OneOrMoreOrdered()) = ONE_MORE_ORD().
  multOfMultOrd(ZeroOrMoreOrdered()) = ZERO_MORE_ORD().
  
  mult : MULT * MULT
  mult(ONE(), ZERO_ONE()).
  mult(ONE(), ONE_MORE_ORD()).
  mult(ZERO_ONE(), ZERO_MORE_ORD()).
  mult(ONE_MORE_ORD(), ZERO_MORE_ORD()).
  mult(ONE_MORE_ORD(), ONE_MORE()).
  mult(ZERO_MORE_ORD(), ZERO_MORE()).
  mult(ONE_MORE(), ZERO_MORE()).
  
  // transitive
  mult(ONE(), ZERO_MORE_ORD()).
  mult(ONE(), ONE_MORE()).
  mult(ONE(), ZERO_MORE()).
  mult(ZERO_ONE(), ZERO_MORE()).
  mult(ONE_MORE_ORD(), ZERO_MORE()).
  
  // reflexive
  mult(M, M).
  
  equimult : MULT * MULT
  equimult(X, X).
  
  multLub : MULT * MULT -> MULT
  
  multLub(X, X) = X.
  multLub(X, Y) = multLub(Y, X).
  
  // identity
//  multLub(ONE(), ONE()) = ONE().
//  multLub(ZERO_ONE(), ZERO_ONE()) = ZERO_ONE().
//  multLub(ONE_MORE_ORD(), ONE_MORE_ORD()) = ONE_MORE_ORD().
//  multLub(ZERO_MORE_ORD(), ZERO_MORE_ORD()) = ZERO_MORE_ORD().
//  multLub(ONE_MORE(), ONE_MORE()) = ONE_MORE().
//  multLub(ZERO_MORE(), ZERO_MORE()) = ZERO_MORE().
  
  // root + descendants
  multLub(ZERO_MORE(), _) = ZERO_MORE().
//  multLub(_, ZERO_MORE()) = ZERO_MORE().
  multLub(ZERO_MORE_ORD(), ONE_MORE()) = ZERO_MORE().
//  multLub(ONE_MORE(), ZERO_MORE_ORD()) = ZERO_MORE().
  
  // zero_more_ord + descendants
  multLub(ZERO_MORE_ORD(), ZERO_ONE()) = ZERO_MORE_ORD().
//  multLub(ZERO_ONE(), ZERO_MORE_ORD()) = ZERO_MORE_ORD().
  multLub(ZERO_MORE_ORD(), ONE_MORE_ORD()) = ZERO_MORE_ORD().
//  multLub(ONE_MORE_ORD(), ZERO_MORE_ORD()) = ZERO_MORE_ORD().
  multLub(ZERO_MORE_ORD(), ONE()) = ZERO_MORE_ORD().
//  multLub(ONE(), ZERO_MORE_ORD()) = ZERO_MORE_ORD().
  
  // one_more + descendants
  multLub(ONE_MORE(), ZERO_ONE()) = ONE_MORE().
//  multLub(ZERO_ONE(), ONE_MORE()) = ONE_MORE().
  multLub(ONE_MORE(), ONE_MORE_ORD()) = ONE_MORE().
//  multLub(ONE_MORE_ORD(), ONE_MORE()) = ONE_MORE().
  multLub(ONE_MORE(), ONE()) = ONE_MORE().
//  multLub(ONE(), ONE_MORE()) = ONE_MORE().
  
  // zero_one + descendant
  multLub(ONE(), ZERO_ONE()) = ZERO_ONE().
//  multLub(ZERO_ONE(), ONE()) = ZERO_ONE().
  
  // one_more_ord + descendant
  multLub(ONE(), ONE_MORE_ORD()) = ONE_MORE_ORD().
//  multLub(ONE_MORE_ORD(), ONE()) = ONE_MORE_ORD().
  
  zeroness : Exp -> ZERONESS
  
  zeroness(Literal(Float("0.0"))) = POSSIBLY_ZERO().
  zeroness(Literal(Float(i))) = NON_ZERO().
  zeroness(Literal(Int("0"))) = POSSIBLY_ZERO().
  zeroness(Literal(Int(_))) = NON_ZERO().
  zeroness(_) = POSSIBLY_ZERO().
  
  multupper : MULT -> MULTU
  multupper(ZERO_ONE()) = UONE().
  multupper(ONE()) = UONE().
  multupper(ZERO_MORE()) = UMANY().
  multupper(ONE_MORE()) = UMANY().
  multupper(ZERO_MORE_ORD()) = UMANY().
  multupper(ONE_MORE_ORD()) = UMANY().
  
  divisionMult : MULT * ZERONESS -> MULT
  divisionMult(ZERO_ONE(), _) = ZERO_ONE().
  divisionMult(ZERO_MORE(), _) = ZERO_MORE().
  divisionMult(ONE(), NON_ZERO()) = ONE().
  divisionMult(ONE(), POSSIBLY_ZERO()) = ZERO_ONE().
  divisionMult(ONE_MORE(), NON_ZERO()) = ONE_MORE().
  divisionMult(ONE_MORE(), POSSIBLY_ZERO()) = ZERO_MORE().
  
  upperboundOne : MULT -> MULT
  
  upperboundOne(ZERO_ONE()) = ZERO_ONE().
  upperboundOne(ZERO_MORE()) = ZERO_ONE().
  upperboundOne(ZERO_MORE_ORD()) = ZERO_ONE().
  upperboundOne(ONE()) = ONE().
  upperboundOne(ONE_MORE()) = ONE().
  upperboundOne(ONE_MORE_ORD()) = ONE().
  
  lowerboundZero : MULT -> MULT
  
  lowerboundZero(ZERO_ONE()) = ZERO_ONE().
  lowerboundZero(ZERO_MORE()) = ZERO_MORE().
  lowerboundZero(ZERO_MORE_ORD()) = ZERO_MORE_ORD().
  lowerboundZero(ONE()) = ZERO_ONE().
  lowerboundZero(ONE_MORE()) = ZERO_MORE().
  lowerboundZero(ONE_MORE_ORD()) = ZERO_MORE_ORD().
  
  toOrdered : MULT -> MULT
  
  toOrdered(ZERO_ONE()) = ZERO_ONE().
  toOrdered(ZERO_MORE()) = ZERO_MORE_ORD().
  toOrdered(ZERO_MORE_ORD()) = ZERO_MORE_ORD().
  toOrdered(ONE()) = ZERO_ONE().
  toOrdered(ONE_MORE()) = ZERO_MORE_ORD().
  toOrdered(ONE_MORE_ORD()) = ZERO_MORE_ORD().
  
  choiceLeftMult : MULT * MULT -> MULT

  choiceLeftMult(ONE(), _) = ONE(). // [1,_] -> always left-hand side operand
  choiceLeftMult(ONE_MORE() , _) = ONE_MORE(). // [1,_] -> always left-hand side operand
  choiceLeftMult(ONE_MORE_ORD() , _) = ONE_MORE_ORD(). // [1,_] -> always left-hand side operand
  choiceLeftMult(ZERO_ONE() , mult) = mult. // [0,1] -> always right-hand side multiplicity
  choiceLeftMult(_, ZERO_MORE()) = ZERO_MORE(). // [0,_] <+ [0,n) unordered -> always [0,n) unordered
  choiceLeftMult(_, ONE_MORE() ) = ONE_MORE(). // [0,_] <+ [1,n) unordered -> always [1,n) unordered
  choiceLeftMult(ZERO_MORE_ORD(), ZERO_ONE() ) = ZERO_MORE_ORD(). // [0,n) ordered -> upper-bound many
  choiceLeftMult(ZERO_MORE_ORD(), ONE()) = ONE_MORE_ORD(). // [0,n) ordered -> upper-bound many
  choiceLeftMult(ZERO_MORE_ORD(), ZERO_MORE_ORD()) = ZERO_MORE_ORD(). // [0,n) ordered -> upper-bound many
  choiceLeftMult(ZERO_MORE_ORD(), ONE_MORE_ORD() ) = ONE_MORE_ORD(). // [0,n) ordered -> upper-bound many
  choiceLeftMult(ZERO_MORE(), ZERO_ONE() ) = ZERO_MORE(). // [0,n) unordered -> upper-bound many, drop ordering
  choiceLeftMult(ZERO_MORE(), ONE()) = ONE_MORE(). // [0,n) unordered -> upper-bound many, drop ordering
  choiceLeftMult(ZERO_MORE(), ZERO_MORE_ORD()) = ZERO_MORE(). // [0,n) unordered -> upper-bound many, drop ordering
  choiceLeftMult(ZERO_MORE(), ONE_MORE_ORD() ) = ONE_MORE(). // [0,n) unordered -> upper-bound many, drop ordering
    
  mergeMult : MULT * MULT -> MULT
  
  mergeMult(ONE_MORE(), _) = ONE_MORE(). // [1,_] unordered -> [1,n) unordered
  mergeMult(_, ONE_MORE()) = ONE_MORE(). // [1,_] unordered -> [1,n) unordered
  mergeMult(ZERO_MORE() , ONE()) = ONE_MORE(). // [0,n) unordered ++ [1,_] -> [1,n) unordered
  mergeMult(ONE(), ZERO_MORE() ) = ONE_MORE(). // [0,n) unordered ++ [1,_] -> [1,n) unordered
  mergeMult(ZERO_MORE() , ONE_MORE_ORD() ) = ONE_MORE(). // [0,n) unordered ++ [1,_] -> [1,n) unordered
  mergeMult(ONE_MORE_ORD() , ZERO_MORE() ) = ONE_MORE(). // [0,n) unordered ++ [1,_] -> [1,n) unordered
  mergeMult(ZERO_MORE() , _) = ZERO_MORE(). // [0,n) unordered remaining -> [0,n) unordered
  mergeMult(_, ZERO_MORE()) = ZERO_MORE(). // [0,n) unordered remaining -> [0,n) unordered
  mergeMult(ZERO_ONE(), ZERO_ONE()) = ZERO_MORE_ORD(). // [0,_] ordered ++ [0,_] ordered -> [0,n) ordered
  mergeMult(ZERO_ONE(), ZERO_MORE_ORD()) = ZERO_MORE_ORD(). // [0,_] ordered ++ [0,_] ordered -> [0,n) ordered
  mergeMult(ZERO_MORE_ORD(), ZERO_ONE()) = ZERO_MORE_ORD(). // [0,_] ordered ++ [0,_] ordered -> [0,n) ordered
  mergeMult(ZERO_MORE_ORD(), ZERO_MORE_ORD()) = ZERO_MORE_ORD(). // [0,_] ordered ++ [0,_] ordered -> [0,n) ordered
  mergeMult(_ , _ ) = ONE_MORE_ORD(). // rest -> [1,n) ordered

  multLift : MULT * MULT -> MULT //def * in -> outFactor

  multLift(_, ONE()) = ONE(). // in~[1,1] fits in all multiplicities
  multLift(ONE(), mult) = mult. // def~[1,1] lifts all multiplicities to in multiplicity
  multLift(ZERO_MORE(), _ ) = ONE(). // def~[0,n) fits all multiplicities
  multLift(ZERO_MORE_ORD(), _) = ONE(). // def~[0,n) fits all multiplicities
  multLift(ZERO_ONE(), ZERO_ONE()) = ONE(). // fits
  multLift(ZERO_ONE(), ZERO_MORE_ORD()) = ONE_MORE_ORD(). // lift upper
  multLift(ZERO_ONE(), ONE_MORE_ORD()) = ONE_MORE_ORD(). // lift upper
  multLift(ZERO_ONE(), ZERO_MORE()) = ONE_MORE(). // lift upper, lose ordering
  multLift(ZERO_ONE(), ONE_MORE()) = ONE_MORE(). // lift upper, lose ordering
  multLift(ONE_MORE(), ONE_MORE()) = ONE(). // fits
  multLift(ONE_MORE(), ONE_MORE_ORD() ) = ONE(). // fits
  multLift(ONE_MORE(), ZERO_ONE()) = ZERO_ONE(). // lift lower
  multLift(ONE_MORE(), ZERO_MORE()) = ZERO_ONE(). // lift lower
  multLift(ONE_MORE(), ZERO_MORE_ORD()) = ZERO_ONE(). // lift lower
  multLift(ONE_MORE_ORD(), ONE_MORE_ORD()) = ONE(). // fits
  multLift(ONE_MORE_ORD(), ONE_MORE()) = ONE_MORE(). // fits, lose ordering (means lift upper as well)
  multLift(ONE_MORE_ORD(), ZERO_ONE()) = ZERO_ONE(). // lift lower
  multLift(ONE_MORE_ORD(), ZERO_MORE_ORD()) = ZERO_ONE(). // lift lower
  multLift(ONE_MORE_ORD(), ZERO_MORE()) = ZERO_MORE(). // lift lower, lose ordering (means lift upper as well)
  
rules // strategy lattice

  //         TOnDemandEventual()
  //              /      \
  //      TEventual()   TOnDemand()
  //              \      /
  //       TOnDemandIncremental()
  //                 |
  //            TIncremental()
  
  stratOfStratList : list(Strategy) -> STRAT
  stratOfStratList([x]) = stratOfStrat(x).
  stratOfStratList([]) = EVENTUAL(). // todo inherit from entity / module
  
  stratOfStrat : Strategy -> STRAT
  stratOfStrat(Incremental()) = INCREMENTAL().
  stratOfStrat(OnDemand()) = ONDEMAND().
  stratOfStrat(OnDemandIncremental()) = ONDEMAND_INCREMENTAL().
  stratOfStrat(Eventual()) = EVENTUAL().
  stratOfStrat(OnDemandEventual()) = ONDEMAND_EVENTUAL().
  // todo what about inline?
  
  strat : STRAT * STRAT
  strat(INCREMENTAL(), ONDEMAND_INCREMENTAL()).
  strat(ONDEMAND_INCREMENTAL(), EVENTUAL()).
  strat(ONDEMAND_INCREMENTAL(), ONDEMAND()).
  strat(ONDEMAND(), ONDEMAND_EVENTUAL()).
  strat(EVENTUAL(), ONDEMAND_EVENTUAL()).
  
  // transitive
  strat(ONDEMAND_INCREMENTAL(), ONDEMAND_EVENTUAL()).
  strat(INCREMENTAL(), EVENTUAL()).
  strat(INCREMENTAL(), ONDEMAND()).
  strat(INCREMENTAL(), ONDEMAND_EVENTUAL()).
  
  // reflexive
  strat(S, S).
  
  equistrat : STRAT * STRAT
  equistrat(S, S).
  
  stratLub : STRAT * STRAT -> STRAT
  
  // root
  stratLub(ONDEMAND_EVENTUAL(), _) = ONDEMAND_EVENTUAL().
  stratLub(_, ONDEMAND_EVENTUAL()) = ONDEMAND_EVENTUAL().
  
  // leaf
  stratLub(INCREMENTAL(), S) = S.
  stratLub(S, INCREMENTAL()) = S.
  
  // inner
  stratLub(EVENTUAL(), ONDEMAND()) = ONDEMAND_EVENTUAL().
  stratLub(ONDEMAND(), EVENTUAL()) = ONDEMAND_EVENTUAL().
  
  stratLub(ONDEMAND_INCREMENTAL(), EVENTUAL()) = EVENTUAL().
  stratLub(EVENTUAL(), ONDEMAND_INCREMENTAL()) = EVENTUAL().
  
  stratLub(ONDEMAND(), ONDEMAND_INCREMENTAL()) = ONDEMAND().
  stratLub(ONDEMAND_INCREMENTAL(), ONDEMAND()) = ONDEMAND().
  
  // identity
  stratLub(ONDEMAND(), ONDEMAND()) = ONDEMAND().
  stratLub(EVENTUAL(), EVENTUAL()) = EVENTUAL().
  stratLub(ONDEMAND_INCREMENTAL(), ONDEMAND_INCREMENTAL()) = ONDEMAND_INCREMENTAL().
    
  materialization : STRAT * SIDE -> MATERIALIZATION
  
  materialization(_, NONE()) = MATERIALIZED().
  materialization(_, LEFT()) = MATERIALIZED().
  materialization(INCREMENTAL(), RIGHT()) = MATERIALIZED().
  materialization(EVENTUAL(), RIGHT()) = MATERIALIZED().
  materialization(ONDEMAND(), RIGHT()) = NON_MATERIALIZED().
  materialization(ONDEMAND_INCREMENTAL(), RIGHT()) = NON_MATERIALIZED().
  materialization(ONDEMAND_EVENTUAL(), RIGHT()) = NON_MATERIALIZED().
  
rules // derivation type lattice

  //       Abstract()
  //        /      \ 
  //  Normal()    Derivation()
  //        \      /
  //     DefaultValue()
  
  deriv : DERIV * DERIV
  deriv(DEFAULT_VALUE(), NORMAL()).
  deriv(DEFAULT_VALUE(), DERIVATION()).
  deriv(DEFAULT_VALUE(), ABSTRACT()).
  deriv(NORMAL(), ABSTRACT()).
  deriv(DERIVATION(), ABSTRACT()).
  deriv(D, D).
  
  derivLub : DERIV * DERIV -> DERIV
  derivLub(D, D) = D.
  derivLub(DEFAULT_VALUE(), X) = X.
  derivLub(X, DEFAULT_VALUE()) = X.
  derivLub(ABSTRACT(), X) = ABSTRACT().
  derivLub(X, ABSTRACT()) = ABSTRACT().
  derivLub(NORMAL(), DERIVATION()) = ABSTRACT().
  derivLub(DERIVATION(), NORMAL()) = ABSTRACT().
  
rules // types

  equitype : TYPE * TYPE
  
  equitype(T, T).
  
  subtype : TYPE * TYPE
  
  subtype(T, T).
  subtype(NOVALUE(), _).
  subtype(T1@ENTITY(_, _), T2@ENTITY(_, _)) :- subtypeEntity(T1, T2).
  
  lubtype : TYPE * TYPE -> TYPE
  lubtype(X, X) = X.
  lubtype(NOVALUE(), INT()) = INT().
  lubtype(NOVALUE(), FLOAT()) = FLOAT().
  lubtype(NOVALUE(), STRING()) = STRING().
  lubtype(NOVALUE(), BOOLEAN()) = BOOLEAN().
  lubtype(NOVALUE(), DATETIME()) = DATETIME().
  lubtype(NOVALUE(), ENTITY(_, X)) = ENTITY(_, X).
  lubtype(INT(), NOVALUE()) = INT().
  lubtype(FLOAT(), NOVALUE()) = FLOAT().
  lubtype(STRING(), NOVALUE()) = STRING().
  lubtype(BOOLEAN(), NOVALUE()) = BOOLEAN().
  lubtype(DATETIME(), NOVALUE()) = DATETIME().
  lubtype(ENTITY(_, X), NOVALUE()) = ENTITY(_, X).
  lubtype(ENTITY(_, X), ENTITY(_, X)) = ENTITY(_, X).
  lubtype(T1@ENTITY(_, s1), T2@ENTITY(_, s2)) = ENTITY(_, s3) :- {scopes1 scopes2 shared_scopes}
    scopes1 == superScopes(s1),
    scopes2 == superScopes(s2),
    shared_scopes == intersection(scopes1, scopes2),
    [s3 | _] == shared_scopes.
  
  cast : TYPE * TYPE
  cast(INT(), FLOAT()).
  cast(INT(), NOVALUE()).
  cast(FLOAT(), INT()).
  cast(FLOAT(), NOVALUE()).
  cast(STRING(), INT()).
  cast(STRING(), FLOAT()).
  cast(STRING(), BOOLEAN()).
  cast(STRING(), DATETIME()).
  cast(STRING(), NOVALUE()).
  cast(STRING(), STRING()).
  
  subtractionType : TYPE -> TYPE
  
  subtractionType(INT()) = INT().
  subtractionType(FLOAT()) = FLOAT().
  subtractionType(DATETIME()) = INT().
  subtractionType(NOVALUE()) = NOVALUE().
  
  divisionType : TYPE -> TYPE
  
  divisionType(_) = FLOAT().

  isNumeric : TYPE
  isNumeric(INT()).
  isNumeric(FLOAT()).
  isNumeric(NOVALUE()).
  
  isString : TYPE
  isString(STRING()).
  isString(NOVALUE()).
  
  isBoolean : TYPE
  isBoolean(BOOLEAN()).
  isBoolean(NOVALUE()).
  
  canCompare : TYPE
  canCompare(DATETIME()).
  canCompare(FLOAT()).
  canCompare(INT()).
  canCompare(STRING()).
  canCompare(NOVALUE()).
  
  canCompare2 : TYPE
  canCompare2(DATETIME()).
  canCompare2(FLOAT()).
  canCompare2(INT()).
  canCompare2(NOVALUE()).
  
  canAdd : TYPE
  canAdd(FLOAT()).
  canAdd(INT()).
  canAdd(STRING()).
  canAdd(NOVALUE()).
  
  canDivide : TYPE
  canDivide(FLOAT()).
  canDivide(INT()).
  canDivide(NOVALUE()).
  
  // TODO: check if still necessary, maybe not due to difference between statix and nabl2 
  isType : TYPE
  isType(BOOLEAN()).
  isType(DATETIME()).
  isType(FLOAT()).
  isType(INT()).
  isType(STRING()).
  isType(NOVALUE()).
  
rules // type checking

  typeOfExp : scope * Exp -> TYPE
  
  typeOfExp(s, e) = T :-
    richTypeOfExp(s, e) == RICHTYPE(T, _, _).

  richTypeOfExp : scope * Exp -> RICHTYPE
  
  typeOfAnyType : scope * AnyType -> TYPE
  
  typeOfAnyType(_, TypePrimitive(t)) = typeOfPrimitiveType(t).
  typeOfAnyType(s, TypeEntity(x)) = typeOfEntityType(s, x).
  
  typeOfPrimitiveType : PrimitiveType -> TYPE
  
  typeOfPrimitiveType(Boolean()) = BOOLEAN().
  typeOfPrimitiveType(Float()) = FLOAT().
  typeOfPrimitiveType(Datetime()) = DATETIME().
  typeOfPrimitiveType(Int()) = INT().
  typeOfPrimitiveType(String()) = STRING().
  
  typeOfEntityType : scope * EntityType -> TYPE
  
  typeOfEntityType(s, EntityType(id)) = type :-
  	(_, type) == resolveEntity(s, id).
  	
  richTypeOfLitVal : LitVal -> RICHTYPE
  richTypeOfLitVal(Int(_)) = RICHTYPE(INT(), ONE(), INCREMENTAL()).
  richTypeOfLitVal(Float(_)) = RICHTYPE(FLOAT(), ONE(), INCREMENTAL()).
  richTypeOfLitVal(True()) = RICHTYPE(BOOLEAN(), ONE(), INCREMENTAL()).
  richTypeOfLitVal(False()) = RICHTYPE(BOOLEAN(), ONE(), INCREMENTAL()).
  richTypeOfLitVal(String(_)) = RICHTYPE(STRING(), ONE(), INCREMENTAL()).
  richTypeOfLitVal(Datetime(_)) = RICHTYPE(DATETIME(), ONE(), INCREMENTAL()).
  richTypeOfLitVal(NoValue()) = RICHTYPE(NOVALUE(), ONE(), INCREMENTAL()).
  
  typeOfLitVal : LitVal -> TYPE
  typeOfLitVal(litval) = t :-
  	RICHTYPE(t, _, _) == richTypeOfLitVal(litval). 
    
rules // name resolution

  declareEntity : scope * ID * scope
  
  declareEntity(s, id, s_model) :-
    !entity[id, ENTITY(id, s_model)] in s.

  resolveEntity : scope * ID -> (ID * TYPE)
  
  resolveEntity(s, ref_id) = (decl_id, type) :-
    query entity
      filter PARENT* and { x' :- x' == ref_id }
        min and true
        in s |-> [(_, (decl_id, type))].
       
  nameOfEntityType : TYPE -> ID
  nameOfEntityType(ENTITY(id, _)) = id.
        
  resolveEntityExtensions : scope * ID -> list((path * (string * scope)))
     
  resolveEntityExtensions(s, id) = ps :-
    query entity_extension
      filter PARENT* and { x' :- x' == (id, _) }
        min and true
        in s |-> ps.
     
  assertMemberOverrideOk : INLINING * DERIV * scope * ID
  assertMemberOverrideOk(own_inlining, own_deriv, s, id) :- { overridden_members }
  	overridden_members == resolveOverriddenMembers(s, id),
  	assertOverriddenMembersOk(own_inlining, own_deriv, id, overridden_members).
  	
  assertOverriddenMembersOk : INLINING * DERIV * ID * list((path * (ID * (RICHTYPE * PROPS))))
  assertOverriddenMembersOk(_, _, _, []).
  assertOverriddenMembersOk(own_inlining, own_deriv, id, [(_, (decl_id, (_, props@PROPS(s_props))))]) :-
  	{ overridden_deriv }
  	@id.ref := decl_id,
  	own_inlining != INLINE() | error $[cannot override [decl_id] with inline attribute]@id,
  	PROPVAL_INLINING(NOT_INLINE()) == getProp(props, "inlining") | error $[cannot override inline attribute [decl_id]]@id,
  	PROPVAL_DERIV(overridden_deriv) == getProp(props, "derivationtype"),
  	deriv(own_deriv, overridden_deriv) | error $[overriding derivation type error]@id.
  	
//  overriddenMemberMult : scope * (ID * (RICHTYPE * PROPS)) -> MULT
//  overriddenMemberMult(s, this_member) = overridden_mult :-
//  	(_, (RICHTYPE(_, overridden_mult, _), _)) ==
//    	resolveOverriddenMember(s, this_member).
//  assertOverriddenMemberMultEqual : scope * (ID * (RICHTYPE * PROPS))
//  assertOverriddenMemberMultEqual(s, this_member@(_, (RICHTYPE(_, this_mult, _), _))) :-
//  	(_, (RICHTYPE(_, this_mult, _), _)) ==
//    	resolveOverriddenMember(s, this_member).
  
  overriddenMemberType : scope * ID * TYPE -> TYPE
  overriddenMemberType(s, id, type) = result :- { overridden_members }
  	overridden_members == resolveOverriddenMembers(s, id),
  	result == overriddenMemberTypeFromList(type, overridden_members).
  overriddenMemberTypeFromList : TYPE * list((path * (ID * (RICHTYPE * PROPS)))) -> TYPE
  overriddenMemberTypeFromList(this_type, []) = this_type.
  overriddenMemberTypeFromList(_, [(_, (_, (RICHTYPE(overridden_type, _, _), _)))]) = overridden_type.

  overriddenMemberMult : scope * ID * MULT -> MULT
  overriddenMemberMult(s, id, mult) = result :- { overridden_members }
  	overridden_members == resolveOverriddenMembers(s, id),
  	result == overriddenMemberMultFromList(mult, overridden_members).
  overriddenMemberMultFromList : MULT * list((path * (ID * (RICHTYPE * PROPS)))) -> MULT
  overriddenMemberMultFromList(this_mult, []) = this_mult.
  overriddenMemberMultFromList(_, [(_, (_, (RICHTYPE(_, overridden_mult, _), _)))]) = overridden_mult.
  
  overriddenMemberStrat : scope * ID * STRAT -> STRAT
  overriddenMemberStrat(s, id, strat) = result :- { overridden_members }
  	overridden_members == resolveOverriddenMembers(s, id),
  	result == overriddenMemberStratFromList(strat, overridden_members).
  overriddenMemberStratFromList : STRAT * list((path * (ID * (RICHTYPE * PROPS)))) -> STRAT
  overriddenMemberStratFromList(this_strat, []) = this_strat.
  overriddenMemberStratFromList(_, [(_, (_, (RICHTYPE(_, _, overridden_strat), _)))]) = overridden_strat.

  assertOverriddenMemberTypeEqual : scope * ID * TYPE
  assertOverriddenMemberTypeEqual(s, id, type) :- { overridden_members }
  	type == overriddenMemberType(s, id, type).
  assertOverriddenMemberTypeEqualFromList : TYPE * list((path * (ID * (RICHTYPE * PROPS))))
  assertOverriddenMemberTypeEqualFromList(_, []).
  assertOverriddenMemberTypeEqualFromList(this_type, [(_, (_, (RICHTYPE(this_type, _, _), _)))]).
  	
  resolveOverriddenMember : scope * ID -> list((ID * (RICHTYPE * PROPS)))
  resolveOverriddenMember(s, id) = result :- { overridden_members }
  	overridden_members == resolveOverriddenMembers(s, id),
  	result == overriddenMemberFromList(overridden_members).
  
  overriddenMemberFromList : list((path * (ID * (RICHTYPE * PROPS)))) -> list((ID * (RICHTYPE * PROPS)))
  overriddenMemberFromList([]) = [].
  overriddenMemberFromList([(_, overridden_member)|_]) = [overridden_member].
     
  resolveOverriddenMembers : scope * ID -> list((path * (ID * (RICHTYPE * PROPS))))
  resolveOverriddenMembers(s, ref_id) = result :-
    query member
      filter PARENT* EXTEND? INHERIT+ EXTEND? and { x' :- x' == ref_id }
        min $ < EXTEND, $ < INHERIT, EXTEND < INHERIT and true
        in s |-> result.
     
  resolveMember : scope * ID -> (ID * RICHTYPE) 
  resolveMember(s, ref_id) = (decl_id, richtype) :-
    query member
      filter PARENT* EXTEND? INHERIT* EXTEND? and { x' :- x' == ref_id }
        min $ < EXTEND, $ < INHERIT, EXTEND < INHERIT and true
        in s |-> [(_, (decl_id, (richtype, _)))|_].
       
  resolveMemberWithProps : scope * ID -> (ID * RICHTYPE * PROPS) 
  resolveMemberWithProps(s, ref_id) = (decl_id, richtype, props) :-
    query member
      filter PARENT* EXTEND? INHERIT* EXTEND? and { x' :- x' == ref_id }
        min $ < EXTEND, EXTEND < INHERIT and true
        in s |-> [(_, (decl_id, (richtype, props)))|_].
        
  assertMemberDeclarationUnique : scope * ID
  assertMemberDeclarationUnique(s, id) :-
  	query member
      filter PARENT* EXTEND? EXTEND? and { x' :- x' == id }
        in s |-> [_]
        | error $[duplicate declaration of member [id]]@id.
  
  declareFunction : scope * ID * RICHTYPE * PROPS
  
  declareFunction(s, id, richtype, props) :-
    !function[id, (richtype, props)] in s,
    assertFunctionDeclarationUnique(s, id).

  resolveFunction : scope * ID -> (ID * RICHTYPE * PROPS)
  
  resolveFunction(s, ref_id) = (decl_id, richtype, props) :-
    query function
      filter PARENT* and { x' :- x' == ref_id }
        min and true
        in s |-> [(_, (decl_id, (richtype, props)))].
     
  assertFunctionDeclarationUnique : scope * ID
  assertFunctionDeclarationUnique(s, id) :-
  	query function
      filter e and { x' :- x' == id }
        in s |-> [_]
        | error $[duplicate declaration of function [id]]@id.
        
  resolveThisType : scope -> TYPE
  resolveThisType(s) = ENTITY(_, s). // todo actually declare "this" and resolve it
  
  declareThisRichtype : scope * RICHTYPE
  declareThisRichtype(s, richtype) :- { s_props }
  	new s_props,
  	!implicit["this", (richtype, PROPS(s_props))] in s.
  
  resolveThisRichtype : scope -> RICHTYPE
  resolveThisRichtype(s) = richtype :-
    query implicit
      filter EXTEND? and { x' :- x' == "this" }
      	min $ < EXTEND and true
        in s |-> [(_, (_, (richtype, _)))|_].
        
  declareMembervalueType : scope * TYPE
  declareMembervalueType(s, t) :- { s_props }
  	new s_props,
  	!implicit["membervalue", (RICHTYPE(t, ONE(), INCREMENTAL()), PROPS(s_props))] in s.
  
  resolveMembervalueType : scope -> TYPE
  resolveMembervalueType(s) = t :-
    query implicit
      filter e and { x' :- x' == "membervalue" }
        in s |-> [(_, (_, (RICHTYPE(t, _, _), _)))|_].
        
  declareMembervalueTypeWithProps : scope * TYPE * PROPS
  declareMembervalueTypeWithProps(s, t, props) :-
  	!implicit["membervalue", (RICHTYPE(t, ONE(), INCREMENTAL()), props)] in s.
  
  resolveMembervalueTypeWithProps : scope -> (TYPE * PROPS)
  resolveMembervalueTypeWithProps(s) = (t, props) :-
    query implicit
      filter e and { x' :- x' == "membervalue" }
        in s |-> [(_, (_, (RICHTYPE(t, _, _), props)))|_].
  
  declareInheritance : scope * ID * list(OptionalExtends)
  
  declareInheritance(s_entity, _, []).
  declareInheritance(s_entity, entity_id, [extends_decl@OptionalExtends(node@Extends(parent_entity))]) :- { s_parent parent_decl_id }
    resolveEntity(s_entity, parent_entity) == (parent_decl_id, ENTITY(_, s_parent)),
    s_entity -INHERIT-> s_parent,
    @parent_entity.ref := parent_decl_id,
    // inheritance cyle detection: reflexive case
    entity_id != parent_entity | error $[entity [entity_id] cannot inherit from itself]@extends_decl,
    // inheritance cycle detection: symmetric and transitive cases
    assertNoCyclicInheritance(s_entity, s_parent) | error $[inheritance is not allowed to be cyclic]@extends_decl.
    
  assertNoCyclicInheritance : scope * scope
  assertNoCyclicInheritance(s_entity, s_parent) :-
  	s_entity != s_parent,
    query ()
    	filter INHERIT+ and { s :- s == s_entity }
		min $ < INHERIT
	  	in s_parent |-> []. 
   
  subtypeEntity: TYPE * TYPE
  subtypeEntity(ENTITY(_, x), ENTITY(_, x)).
  subtypeEntity(ENTITY(_, s_entity), ENTITY(_, s_super)) :- {results}
  	query ()
  		filter INHERIT+ and true
  		min $ < INHERIT and true
  		in s_entity |-> results,
  	includesScope(results, s_super).
  	
  superScopes: scope -> list((path * scope))
  superScopes(s) = results :-
  	query ()
  		filter INHERIT+ and true
  		min $ < INHERIT and true
  		in s |-> results.
  
  intersection: list((path * scope)) * list((path * scope)) -> list(scope)
  intersection([(_, x) | xtail], [(_, x) | ytail]) = [x | intersection(xtail, ytail)].
  intersection([(_, x)], [(_, x) | tail]) = [x].
  intersection([(_, x) | tail], [(_, x)]) = [x].
  intersection([(_, x)], [(_, x)]) = [x].
  
  intersection([(_, x) | tail], ys) = concat(intersection([(_, x)], ys), intersection(tail, ys)).
  intersection([(_, x)], [_ | tail]) = intersection([(_, x)], tail).
  
  intersection([(_, x) | tail], []) = [].
  intersection([(_, x)], []) = [].
  intersection([], [(_, x) | tail]) = [].
  intersection([], [(_, x)]) = [].
  intersection([(_, x)], [(_, y)]) = [].
  
  intersection(_, []) = [].
  intersection([], _) = [].
  intersection([], []) = [].
  
  concat: list(scope) * list(scope) -> list(scope)
  concat([x | xs], ls) = [x | concat(xs, ls)].
  concat(ls, []) = ls.
  concat([], ls) = ls.
  
  includesScope : list((path * scope)) * scope
  includesScope([(_, x) | _], x).
  includesScope([_ | xs], x) :- includesScope(xs, x).
  
  
rules // partial classes / extension
  
  extendScopes maps extendScope(list(*), *)
  extendScope : (path * (string * scope)) * scope
  extendScope((_, (_, s)), s).
  extendScope((_, (_, s')), s) :- s -EXTEND-> s'.
  
//  declareScopes maps declareScope(list(*), *)
//  declareScope : (path * (string * TYPE)) * scope
//  declareScope((_, (_, ENTITY(_, s))), s).
//  declareScope((_, (_, ENTITY(_, s'))), s) :- s -DECLARE-> s'.
  
rules // props


  initProps : scope * scope -> PROPS
  initProps(s_context, s_props) = PROPS(s_props) :-
  	extendPropScopes(resolvePropExtensions(s_context, s_props), s_props).
  
  extendProps : scope * scope * scope
  extendProps(s_context, s_props, s_extension) :-
  	s_extension -EXTEND-> s_props,
  	!prop_extension[s_props, s_extension] in s_context.

  resolvePropExtensions : scope * scope -> list((path * (scope * scope)))
  resolvePropExtensions(s, s_props) = ps :-
    query prop_extension
      filter PARENT* and { x' :- x' == (s_props, _) }
        min and true
        in s |-> ps.
  extendPropScopes maps extendPropScope(list(*), *)
  extendPropScope : (path * (scope * scope)) * scope
  extendPropScope((_, (_, s)), s).
  extendPropScope((_, (_, s')), s) :- s -EXTEND-> s'.
  
  setProp: scope * string * string
  setProp(s, key, value) :-
    !prop[key, PROPVAL_STRING(value)] in s.
    
  setPropExp: scope * string * Exp
  setPropExp(s, key, exp) :-
    !prop[key, PROPVAL_EXP(exp)] in s.
    
  setPropStrat: scope * string * STRAT
  setPropStrat(s, key, strat) :-
    !prop[key, PROPVAL_STRAT(strat)] in s.
   
  setPropMult: scope * string * MULT
  setPropMult(s, key, mult) :-
    !prop[key, PROPVAL_MULT(mult)] in s.
    
  setPropInlining: scope * string * INLINING
  setPropInlining(s, key, inlining) :-
    !prop[key, PROPVAL_INLINING(inlining)] in s.
    
  setPropDeriv: scope * string * DERIV
  setPropDeriv(s, key, deriv) :-
    !prop[key, PROPVAL_DERIV(deriv)] in s.
    
  setPropType: scope * string * TYPE
  setPropType(s, key, type) :-
    !prop[key, PROPVAL_TYPE(type)] in s.
    
  setPropOptionalType: scope * string * TYPE
  setPropOptionalType(s, key, NOTYPE()).
  setPropOptionalType(s, key, type) :- setPropType(s, key, type).
    
  setPropRichtypes: scope * string * list(RICHTYPE)
  setPropRichtypes(s, key, richtypes) :-
  	!prop[key, PROPVAL_RICHTYPES(richtypes)] in s.
  
  getProp: PROPS * string -> PROPVAL
  getProp(PROPS(s), key) = propval :-
  	query prop
      filter EXTEND? and { x' :- x' == key }
        min and true
        in s |-> [(_, (_, (propval)))].
        
  getPropOptionalType : PROPS * string -> TYPE
  getPropOptionalType(props, key) = type :- { proplist }
  	proplist == getPropList(props, key),
  	PROPVAL_TYPE(type) == getPropOptionalTypeFromList(proplist).
  
  getPropOptionalTypeFromList : list((path * (string * (PROPVAL)))) -> PROPVAL
  getPropOptionalTypeFromList([]) = PROPVAL_TYPE(NOTYPE()).
  getPropOptionalTypeFromList([(_, (_, propval)) | _]) = propval.
  
  getPropList : PROPS * STRING -> list((path * (string * (PROPVAL))))
  getPropList(PROPS(s), key) = proplist :-
  	query prop
      filter EXTEND? and { x' :- x' == key }
        min and true
        in s |-> proplist.
